plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
}

group = 'api'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'javax.persistence:javax.persistence-api:2.2'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.postgresql:postgresql:42.7.2'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql:10.15.2'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    implementation 'io.jsonwebtoken:jjwt-impl:0.12.6'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.12.6'
    implementation 'com.google.cloud:google-cloud-storage:2.40.1'
    implementation 'io.minio:minio:8.5.11'
    implementation 'org.springframework.boot:spring-boot-starter-security:3.3.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    implementation 'com.rabbitmq:amqp-client:5.21.0'
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

springBoot  {
    mainClass = 'api.startup.Program'
}


tasks.register('dynamicRun', JavaExec) {
    if (project.hasProperty('mainClass')) {
        mainClass = project.mainClass
    } else {
        mainClass = springBoot.mainClass
    }
    classpath = sourceSets.main.runtimeClasspath
    systemProperty 'spring.profiles.active', project.hasProperty('springProfiles') ? project.springProfiles : 'default'
    args = project.hasProperty('appArgs') ? project.appArgs.split(',') : []
}

// Set dynamicRun as default task
defaultTasks 'dynamicRun'